pluginManagement {
    repositories {
        maven {
            // RetroFuturaGradle
            name 'GTNH Maven'
            url 'https://nexus.gtnewhorizons.com/repository/public/'
            mavenContent {
                includeGroup 'com.gtnewhorizons'
                includeGroup 'com.gtnewhorizons.retrofuturagradle'
            }
        }
        gradlePluginPortal()
        mavenCentral()
        mavenLocal()
    }
}

plugins {
    // Automatic toolchain provisioning
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.4.0'
}

// Due to an IntelliJ bug, this has to be done
// rootProject.name = archives_base_name
rootProject.name = rootProject.projectDir.getName()

include 'Api'
include 'Plugins'

def pei_plugins = []

def plugins_dir = new File(rootDir, 'Plugins')

def plugins = plugins_dir.listFiles(new FileFilter() {
    @Override
    boolean accept(File pathname) {
        if (!pathname.isDirectory()) {
            return false
        }
        if (pathname.name == 'buildSrc') {
            return false
        }
        return new File(pathname, 'build.gradle').isFile()
    }
})

plugins.each { File plugin ->
    include "Plugins:${plugin.name}"
    project(":Plugins:${plugin.name}").projectDir = new File(plugins_dir, plugin.name)
    pei_plugins += plugin.name
}

gradle.ext.pei_plugins = pei_plugins
